plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

group 'com.agile.engine.challenge'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def vertxVersion = "3.9.4"
def log4j2Version = "2.13.3"
def jacksonVersion = "2.11.2"

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib")

    implementation("io.vertx:vertx-rx-java2:$vertxVersion")
    implementation("io.vertx:vertx-web:$vertxVersion")
    implementation("io.vertx:vertx-config:$vertxVersion")
    implementation("io.vertx:vertx-config-yaml:$vertxVersion")
    implementation("io.vertx:vertx-web-client:$vertxVersion")

    implementation("org.apache.logging.log4j:log4j-api:$log4j2Version")
    implementation("org.apache.logging.log4j:log4j-core:$log4j2Version")
    implementation("org.apache.logging.log4j:log4j-slf4j-impl:$log4j2Version")

    implementation("com.google.inject:guice:4.2.3")

    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")

    testImplementation("io.vertx:vertx-junit5:$vertxVersion") {
        exclude group: "io.reactivex", module: "rxjava"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardError"
    }

    afterSuite { desc, result ->
        if (!desc.parent)
            println("${result.resultType} " +
                    "(${result.testCount} tests, " +
                    "${result.successfulTestCount} successes, " +
                    "${result.failedTestCount} failures, " +
                    "${result.skippedTestCount} skipped)")
    }

}

def mainVerticleName = 'com.agile.engine.challenge.infrastructure.MainVerticle'
tasks.build.dependsOn tasks.shadowJar
shadowJar {
    archiveBaseName = 'agile-engine-challenge'
    archiveClassifier = 'shadow'

    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

mainClassName = 'com.agile.engine.challenge.infrastructure.ApplicationKt'
def watchForChange = 'src/**/*.*'
def doOnChange = './gradlew classes'
run {
    systemProperty "vertx.logger-delegate-factory-class-name", "io.vertx.core.logging.SLF4JLogDelegateFactory"
    args = [
            'run', mainVerticleName,
            "--redeploy=$watchForChange",
            "--launcher-class=$mainClassName",
            "--on-redeploy=$doOnChange"
    ]
}